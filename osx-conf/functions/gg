# vim: ft=zsh sw=2 ts=2 expandtab

gg_root() {
  if [[ -f "$HOME/.ggdir" ]]; then
    cat "$HOME/.ggdir"
  else
    printf '%s\n' "$HOME/Projects"
  fi
}

gg() {
  local root target
  root="$(gg_root)"
  target="${1:-}"

  if [[ -z "$target" ]]; then
    cd "$root" || return 1
    return 0
  fi

  cd "$root/$target" || return 1
}

_gg_complete() {
  local root
  root="$(gg_root)"
  compadd -- "$root"/*(/N:t)
}

if [[ -o interactive ]] && (( $+functions[compdef] )); then
  compdef _gg_complete gg
fi
